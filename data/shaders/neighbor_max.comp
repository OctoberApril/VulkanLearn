#version 450

#extension GL_ARB_separate_shader_objects : enable
#extension GL_ARB_shading_language_420pack : enable

#include "uniform_layout.sh"
#include "global_parameters.sh"

layout (local_size_x = 16, local_size_y = 16, local_size_z = 1) in;

layout (set = 3, binding = 0) uniform sampler2D motionTileMax[3];
layout (set = 3, binding = 1, rgba32f) uniform image2D outTileNeighborMax[3];

void main() 
{
	ivec2 size = imageSize(outTileNeighborMax[frameIndex]);
	vec2 uv = vec2(gl_GlobalInvocationID.xy + 0.5f) / vec2(size);

	if (any(greaterThanEqual(gl_GlobalInvocationID.xy, size)))
		return;

	vec2 du = vec2(1.0f / globalData.motionTileWindowSize.z, 0.0f);
	vec2 dv = vec2(0.0f, 1.0f / globalData.motionTileWindowSize.w);

	vec2 maxMotion = vec2(0);
	float maxLength = 0;

	for (int x = -1; x <= 1; x++)
	{
		for (int y = -1; y < 1; y++)
		{
			vec2 motionVec = texture(motionTileMax[frameIndex], uv + x * du + y * dv).rg;
			float len = dot(motionVec, motionVec);
			if (maxLength < len)
			{
				maxLength = len;
				maxMotion = motionVec;
			}
		}
	}

	imageStore(outTileNeighborMax[frameIndex],	
		ivec2(gl_GlobalInvocationID.xy), 
		vec4(maxMotion, 0.0f, 0.0f));
}